% meow/1

struct point2d(x: int, y: int);

test "application" {
  assert 1 + 1 ==> 2;
  assert 1 - 1 ==> 0;
  assert 2 * 2 ==> 4;
  assert 4 \ 2 ==> 2;
  assert 2 ** 3 ==> 8;
  assert 4 % 3 ==> 1;

  assert 1 < 2;
  assert 2 <= 2;
  assert 2 > 1;
  assert 2 >= 2;
  assert 2 === 2;
  assert 1 =/= 2;

  assert 2 << 2 ==> 8;
  assert 8 >> 2 ==> 2;
  assert 8 >>> 8 ==> 0;
  assert 1 | 2 ==> 3;
  assert 1 & 2 ==> 0;
  assert 1 ^ 2 ==> 3;

  assert [1, 2, 3] ++ [4, 5] ==> [1, 2, 3, 4, 5];
  assert [1: 2, 3: 4, ...[3: 5]] ==> [1: 2, 3: 5];
  assert #(a: 1, b: 2) ==> #(b: 2, a: 1);
  assert "hello" ++ "world" ==> "helloworld";

  assert 1 as integer ==> 1;
  
  let X = #cell of(1);
  assert (X <- 2) ==> 1;

  let Z = #cell of([1, 2]);
  let Y = #weak-ref of(Z read());
  assert (Y read() unwrap()) ==> Z read();

  assert 1 =:= 1;
}

test "types" {
  let P = maybe..none;
  let Q = new maybe..some(value: 3);

  assert P is maybe;
  assert P is maybe..none;
  assert Q is maybe..some;

  assert Q.value ==> 3;

  assert new point2d(x: 1, y: 1) is point2d;
  assert (new point2d(x: 1, y: 2)).y ==> 2;
}

test "thunks" {
  let A = lazy (force B);
  let B = lazy 1 + 1;

  assert force A ==> 2;
  assert force B ==> 2;
  assert force A ==> 2 :: repeating;
}

def maybe..some value() { self.value }
def maybe..none value() { nothing }

test "methods" {
  assert maybe..none value() ==> nothing;
  assert new maybe..some(value: 1) value() ==> 1;
}

test "data" {
  assert 1 ==> 1;
  assert 1.0f ==> 1.0f;
  assert 1 ==> 1;
  assert "text" ==> "text";
  assert true ==> true;
  assert false ==> false;
  assert nothing ==> nothing;
  assert [1, 2, 3] ==> [1, 2, 3];
  assert [1: 2, 3: 4] ==> [1: 2, 3: 4];
  assert #integer ==> #integer;
}

test "lambda and application" {
  let F0 = { 1 };
  let F1 = { A in A + 1 };
  let F2 = { A, B in A + B };

  assert F0() ==> 1;
  assert F1(1) ==> 2;
  assert F2(2, 3) ==> 5;

  let A = 1 |> _ + 2 |> #(a: 1, b: _) |> _.b;
  assert A ==> 3;
}

import foreign "./foreign.js"; // node resolution

test "foreign calls" {
  assert foreign hello("hi") ==> "hihi";
}

test "repeat loops" {
  // Functional repeat blocks
  let B = (repeat with A = 1 {
    when {
      A < 10 => continue with A = A + 1;
      otherwise => break with A + 1;
    }
  });

  assert B ==> 11;

  // Regular repeat blocks
  let X = #cell of(1);
  repeat {
    when {
      X read() < 10 => {
        X <- X read() + 1;
        continue;
      }
      otherwise => break;
    }
  }
  assert X read() ==> 10;
}

test "binaries" {
  assert <<>> ==> <<>>;
  assert <<1, 2, 3>> ==> <<1, 2, 3>>;
  assert <<12345:u16le>> ==> <<57, 48>>;
  assert <<12345:u16be>> ==> <<48, 57>>;
  assert <<12345:u32le>> ==> <<57, 48, 0, 0>>;
  assert <<12345:u32be>> ==> <<0,   0, 48, 57>>;
  assert <<12345:u64le>> ==> <<57, 48, 0, 0, 0, 0, 0, 0>>;
  assert <<12345:u64be>> ==> <<0,   0, 0, 0, 0, 0, 48, 57>>;
}

open package meow.codec;
open package meow.codec as c;

test "opening packages" {
  assert #base64 from(<<1, 2, 3, 4>>) encode() ==> "AQIDBA==";
  assert #base64 decode("AQIDBA==") bytes() ==> <<1, 2, 3, 4>>;

  assert #c/base64 from(<<1, 2, 3, 4>>) encode() ==> "AQIDBA==";
  assert #c/base64 decode("AQIDBA==") bytes() ==> <<1, 2, 3, 4>>;
}

// TODO:
// for loops