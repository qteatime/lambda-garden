% meow/1

// -- Constructors -------------------------------------------------------------
def #stream empty() -> stream<A> {
  stream..empty;
}

def #stream from(Items: array<A>) -> stream<A> {
  Items fold-right(from: #stream empty(), with: { X, Tail in
    new stream..cons(head: X, tail: lazy Tail)
  });
} test {
  assert (#stream from([]) as array) ==> [];
  assert (#stream from([1, 2, 3]) as array) ==> [1, 2, 3];
}

def #stream iterate(from Init: A, with Fn: (A) -> A) -> stream<A> {
    new stream..cons(head: Init, tail: lazy #stream iterate(from: Fn(Init), with: Fn));
} test {
  let Naturals = #stream iterate(from: 0, with: {A in A + 1});
  assert (Naturals take(5n) as array) ==> [0, 1, 2, 3, 4];
}

// -- Sequence -----------------------------------------------------------------
def stream..cons<A> first!() -> A {
  self.head;
}

def stream..empty<A> rest() -> stream<A> { self }
def stream..cons<A> rest() -> stream<A> { force self.tail }

// -- Transformations ----------------------------------------------------------
def stream..empty take(Count: i32) -> stream<A> { self }
def stream..cons<A> take(Count: i32) -> stream<A> {
  assert Count >= 0n;
  when {
    Count === 0n => #stream empty();
    otherwise => new stream..cons(head: self first!(), tail: lazy self rest() take(Count - 1n));
  }
} test {
  assert (#stream from([]) take(2n) as array) ==> [];
  assert #stream from([1, 2, 3]) take(2n) as array ==> [1, 2];
  assert #stream from([1, 2, 3]) take(10n) as array ==> [1, 2, 3];
  assert #stream from([1, 2, 3]) take(0n) as array ==> [];
}

def stream..empty drop(Count: i32) -> stream<A> { self }
def stream..cons<A> drop(Count: i32) -> stream<A> {
  assert Count >= 0n;
  when {
    Count === 0n => self;
    otherwise => self rest() drop(Count - 1n);
  }
} test {
  assert #stream from([]) drop(2n) as  array ==> [];
  assert #stream from([1, 2, 3]) drop(2n) as array ==> [3];
  assert #stream from([1, 2, 3]) drop(10n) as array ==> [];
  assert #stream from([1, 2, 3]) drop(0n) as array ==> [1, 2, 3];
}

def stream..empty take-while(Count: i32) -> stream<A> { self }
def stream..cons<A> take-while(Predicate: (A) -> bool) -> stream<A> {
  when {
    Predicate(self first!()) => new stream..cons(head: self first!(), tail: lazy self rest() take-while(Predicate));
    otherwise => #stream empty();
  }
} test {
  let Naturals = #stream iterate(from: 0, with: { A in A + 1 });
  assert Naturals take-while({X in X < 5}) as array ==> [0, 1, 2, 3, 4];
}

def stream..empty drop-while(Count: i32) -> stream<A> { self }
def stream..cons<A> drop-while(Predicate: (A) -> bool) -> stream<A> {
  when {
    not Predicate(self first!()) => self;
    otherwise => self rest() drop-while(Predicate);
  }
} test {
  let Naturals = #stream iterate(from: 0, with: { A in A + 1 });
  assert Naturals drop-while({X in X < 5}) take(5n) as array ==> [5, 6, 7, 8, 9];
}

def stream..empty<A> map(Fn: (A) -> B) -> stream<B> {
  self;
}
def stream..cons<A> map(Fn: (A) -> B) -> stream<B> {
  new stream..cons(head: Fn(self first!()), tail: lazy self rest() map(Fn));
}

def stream..empty<A> keep-if(Fn: (A) -> bool) -> stream<A> {
  self;
}
def stream..cons<A> keep-if(Fn: (A) -> bool) -> stream<A> {
  when {
    Fn(self first!()) => new stream..cons(head: self first!(), tail: lazy self rest() keep-if(Fn));
    otherwise => self rest() keep-if(Fn);
  }
}

def stream<A> remove-if(Fn: (A) -> bool) -> stream<A> {
  self keep-if({X in not Fn(X)});
}

// -- Folds --------------------------------------------------------------------
def stream..empty fold(from Init: B, with Fn: (B, A) -> B) {
  Init;
}
def stream..cons<A> fold(from Init: B, with Fn: (B, A) -> B) -> B {
  Fn(Init, (force self.tail) fold(from: self.head, with: Fn));
}

def stream<A> sum(of Type: #f64) -> f64 {
  self fold(from: 0.0f, with: {A, B in A + B});
}

def stream<A> sum(of Type: #i32) -> i32 {
  self fold(from: 0.0f, with: {A, B in A + B});
}

def stream<A> sum(of Type: #integer) -> integer {
  self fold(from: 0, with: {A, B in A + B});
}

// -- Conversions --------------------------------------------------------------
def stream..empty<A> as array {
  [];
}
def stream..cons<A> as array {
  [self.head, ...(force self.tail) as array];
}