% meow/1

struct base64(bytes: byte-array);

import foreign "./native/base64.js";

def #base64 from(Bytes: byte-array) -> base64 {
  new base64(Bytes);
}

def #base64 decode(Text: text) -> base64 {
  new base64(foreign base64.decode(Text));
} test {
  assert #base64 decode("") bytes() ==> <<>>;
  assert #base64 decode("YQ==") bytes() ==> "a" to-utf8-bytes() unwrap();
  assert #base64 decode("SXQgd2FzIGEgZGFyaywgc3Rvcm15IG5pZ2h0") bytes()
    ==> "It was a dark, stormy night" to-utf8-bytes() unwrap();
}

def base64 bytes() {
  self.bytes;
}

def base64 encode() -> text {
  foreign base64.encode(self.bytes);
} test {
  assert #base64 from(<<>>) encode() ==> "";
  assert #base64 from("a" to-utf8-bytes() unwrap()) encode() ==> "YQ==";
  assert #base64 from("It was a dark, stormy night" to-utf8-bytes() unwrap()) encode()
    ==> "SXQgd2FzIGEgZGFyaywgc3Rvcm15IG5pZ2h0";
}

def base64 === (X: base64) -> bool {
  self.bytes === X.bytes;
}

def base64 =/= (X: base64) -> bool {
  self.bytes =/= X.bytes;
}